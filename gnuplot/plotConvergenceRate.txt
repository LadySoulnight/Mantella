#!/usr/bin/gnuplot

reset

set terminal pngcairo size 336,252 enhanced font 'Verdana,9'
set output 'convergenceRate.png'

# axes
set style line 11 lc rgb '#808080' lt 1
set border 3 front ls 11
set tics nomirror out scale 0.75

set format y '10^{%T}'
set logscale y
set mytics 10    # Makes logscale look good.

# grid
set style line 12 linecolor rgb'#808080' linetype 0 linewidth 1
set grid back linestyle 12
set grid xtics ytics mxtics

set style line 1 linetype 1 linecolor rgb '#DD181F' linewidth 2 pointtype -1
set style line 2 linetype 1 linecolor rgb '#0060AD' linewidth 2 pointtype -1

set xlabel 'Number of fitness evaluations (#FEs)'
set ylabel 'Best fitness value (f)'

set key invert
set key top right

plot \
  'convergenceRateStdandardDeviation.dat' \
    title 'Standard deviation' with linespoints linestyle 2, \
  'convergenceRateMean.dat' \
    title 'Mean' with linespoints linestyle 1
